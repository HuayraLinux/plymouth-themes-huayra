# Gradiente de fondo
gradiente = Image("gradiente.png");

# En el caso de no usarse en una netbook, Huayra debe reconocer y escalar a la resolucion correcta

ancho_pantalla = Window.GetWidth();
altura_pantalla = Window.GetHeight();

grad_rescalado = gradiente.Scale(ancho_pantalla, altura_pantalla);

gradiente_sprite = Sprite(grad_rescalado);

gradiente_sprite.SetX(Window.GetWidth() / 2 - grad_rescalado.GetWidth() / 2);
gradiente_sprite.SetY(Window.GetHeight() / 2 - grad_rescalado.GetHeight() / 2);
gradiente_sprite.SetZ(-100000);

# Carga frames de animacion
for (i = 0; i < 40; i++)
    timer_imagenes[i] = Image("roseta_" + i + ".png");
timer_sprite = Sprite();

timer_sprite.SetX(Window.GetWidth() / 2 - timer_imagenes[1].GetWidth() / 2);
timer_sprite.SetY(Window.GetHeight() * 0.85 - timer_imagenes[1].GetHeight() / 2);
timer_sprite.SetZ(11);
timer_sprite.SetOpacity(0.5);

# Carga logo Huayra
huayra_logo = Image("logo.png");
logo_sprite = Sprite(huayra_logo);

logo_sprite.SetX(Window.GetWidth() / 2 - huayra_logo.GetWidth() / 2);
logo_sprite.SetY(Window.GetHeight() * 0.4 - huayra_logo.GetHeight() / 2);
logo_sprite.SetZ(10);
logo_sprite.SetOpacity(0.2);


# Add your code here
alfa = 0.0;
bucle = 0;
cuadros = 0;

fun refresh_callback()
  {
     timer_sprite.SetImage(timer_imagenes[cuadros]);
     cuadros++;
     if (cuadros > 39){
         cuadros = 0;
         }
     logo_sprite.SetOpacity(alfa/100);
     bucle++;
     if (bucle < 100){     
         alfa++;
         }
     else{
	 alfa--;
	 }
     if(bucle>200){
	 bucle = 0;
         }
  }

Plymouth.SetRefreshFunction (refresh_callback);



#----------------------------------------- Quit --------------------------------

fun quit_callback ()
{
}

Plymouth.SetQuitFunction(quit_callback);


